plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id 'java'
	id 'war'
	id 'org.liquibase.gradle' version '2.1.1'

}

group = 'com.reservation'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.liquibase:liquibase-core'

	runtimeOnly 'mysql:mysql-connector-java'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	liquibaseRuntime 'org.liquibase:liquibase-core'
	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.5.0'
	liquibaseRuntime 'mysql:mysql-connector-java'
	liquibaseRuntime 'org.springframework.boot:spring-boot'
	liquibaseRuntime 'org.springframework:spring-orm'
	liquibaseRuntime 'javax.xml.bind:jaxb-api'
	liquibaseRuntime 'org.hibernate:hibernate-core'
	liquibaseRuntime sourceSets.main.output


}

tasks.named('test') {
	useJUnitPlatform()
}

diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava

liquibase {
	activities {
		main {
			driver 'com.mysql.cj.jdbc.Driver'
			url 'jdbc:mysql://localhost:3306/reservation_local?useSSL=false&allowPublicKeyRetrieval=true'
			username 'root'
			password ''
			changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
			referenceUrl 'hibernate:spring:com.reservation' +
					'?dialect=' +
					'org.hibernate.dialect.MySQL5Dialect' +
					'&hibernate.physical_naming_strategy=' +
					'org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy' +
					'&hibernate.implicit_naming_strategy=' +
					'org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
		}
	}
}

